#!/usr/bin/perl -w
#
# Copyright 2004-2012, Stephen Fryatt
#
# This file is part of ManTools:
#
#   http://www.stevefryatt.org.uk/software/
#
# Licensed under the EUPL, Version 1.1 only (the "Licence");
# You may not use this work except in compliance with the
# Licence.
#
# You may obtain a copy of the Licence at:
#
#   http://joinup.ec.europa.eu/software/page/eupl
#
# Unless required by applicable law or agreed to in
# writing, software distributed under the Licence is
# distributed on an "AS IS" basis, WITHOUT WARRANTIES
# OR CONDITIONS OF ANY KIND, either express or implied.
#
# See the Licence for the specific language governing
# permissions and limitations under the Licence.

# DDFMan -- Convert help-markup into DDF files.
#
# Usage: DDFMan -I<infile> -O<outfile> [-D<var>=<val> ...]

# The output type for literals in the file.

$mode="DDF";

# Formatting variables.

set_new_indent(0);
set_new_spacing(1);
set_new_wrapping(1);
set_new_list_value(0, 0);
$all_lines = 0;

%defs = ("title", "");

# Set the entities that we recognise.  NB: this list must contain them in a plain text form.

%entities = (
	"lt",	"<",
	"gt",	">",
	"quot",	"\"",
	"amp",	"\e"	# & goes to ESC, then is converted back to &
);

if (scalar(@ARGV) > 2) {
	# Too many arguments, so assume the new format.

	foreach $arg (@ARGV) {
		if ($arg =~ /^-I(.+)/) {
			$infile = $1;
		} elsif ($arg =~ /^-O(.+)/) {
			$outfile = $1;
		} elsif ($arg =~ /^-D([^=]+)=(.*)/) {
			$defs{$1} = $2;
		}
	}
} else {
	# Default to the parameters being <infile> <outfile> for backwards
	# compatibility.

	$infile = $ARGV[0];
	$outfile = $ARGV[1];
}

# Open the input and output files -- there is only one output file used.

open(INPUT, $infile) || die "Can't open input file $infile: $!\n";
open(OUTPUT, ">".$outfile) || die "Can't open output file $outfile: $!\n";

# Read the lines in one at a time and process them.

while (defined($line = <INPUT>)) {
	chomp($line);

	# Convert { into their DDF equivalent now.

	$line =~ s/{/{\\123}/g;

	if ($line =~ /^\s*<([^<>]*)>\s*$/) {
		%params = parse_tag($1);

		if ($params{'tag_name'} eq 'define') {
			# If it's already defined, then either we've defined it twice in the file,
			# or the file definition has been overridden by a command line definition.

			if (!defined($defs{$params{'name'}})) {
				$defs{$params{'name'}} = $params{'value'};
			}
		} elsif ($params{'tag_name'} eq 'chapter') {
			print OUTPUT "{nextframe}\n";
			print OUTPUT "{\"Main Heading\" on}".$params{'title'}."{\"Main Heading\" off}\n";
		} elsif ($params{'tag_name'} eq 'literal') {
			while (defined ($literal_line = <INPUT>) && $literal_line !~ /^<\/literal>$/) {
				if (uc($params{'mode'}) eq uc($mode)) {
					while ($literal_line =~ /\$\$([a-z]+?)\$\$/) {
						$literal_line = "$`$defs{$1}$'";
					}

					print OUTPUT $literal_line;
				}
			}
		} elsif ($params{'tag_name'} eq 'subhead') {
			print OUTPUT "{\"Sub Heading\" on}".$params{'title'}."{\"Sub Heading\" off}\n";
		} elsif ($params{'tag_name'} eq 'codeblock') {
			print OUTPUT "{\"Code Block\" on}";
			$all_lines = 1;
		} elsif ($params{'tag_name'} eq '/codeblock') {
			print OUTPUT "{\"Code Block\" off}";
			$all_lines = 0;
		} elsif ($params{'tag_name'} eq 'box') {
			print OUTPUT "{\"Box\" on}";
		} elsif ($params{'tag_name'} eq '/box') {
			print OUTPUT "{\"Box\" off}";
		} elsif ($params{'tag_name'} eq 'list') {
			if (defined($params{'spacing'}) && $params{'spacing'} == 1) {
				set_new_spacing(1);
			} else {
				set_new_spacing (0);
			}

			if (defined($params{'type'})) {
				if ($params{'type'} eq 'num') {
					set_new_list_value(1, 2);
					set_new_indent($indent + 1);
				} elsif ($params{'type'} eq 'upper') {
					set_new_list_value(1, 3);
					set_new_indent($indent + 1);
				} elsif ($params{'type'} eq 'lower') {
					set_new_list_value(1, 4);
					set_new_indent($indent + 1);
				} else {
					set_new_list_value(0, 1);
					set_new_indent($indent + 1);
				}
			} else {
				set_new_list_value(0, 1);
				set_new_indent($indent + 1);
			}

			print OUTPUT "{\"List ".$indent."\" on}";
		} elsif ($params{'tag_name'} eq '/list') {
			print OUTPUT "{\"List ".$indent."\" off}";

			restore_spacing();
			restore_indent();
			restore_list_value();
		} elsif ($params{'tag_name'} eq 'definition') {
			print OUTPUT "{\"Definition Head\" on}".$params{'target'}."{\"Definition Head\" off}\n{\"Definition Body\" on}";
		} elsif ($params{'tag_name'} eq '/definition') {
			print OUTPUT "\"Definition Body\" off}";
		}
	} elsif ($line !~ /^\s*$/  || $all_lines) {
		output_line(OUTPUT, $line, $indent, $spacing);
	}
}



# Process a line, containing any inline tags, and output the result.

sub output_line
{
	my ($output, $line, $indent, $spacing) = @_;

	$line =~ s/<cite>(.*?)<\/cite>/\{"Cite" on}$1\{"Cite" off}/g;			# Citation
	$line =~ s/<code>(.*?)<\/code>/\{"Code" on}$1\{"Code" off}/g;			# Inline code
	$line =~ s/<command>(.*?)<\/command>/\{"Command" on}$1\{"Command" off}/g;	# Inline code - enclose in ""
	$line =~ s/<em>(.*?)<\/em>/\{"Emphasis" on}$1\{"Emphasis" off}/g;		# Emphasis - To Upper
	$line =~ s/<entry>(.*?)<\/entry>/\{"Entry" on}$1\{"Entry" off}/g;		# User entry - enclose in ""
	$line =~ s/<file>(.*?)<\/file>/\{"Filename" on}$1\{"Filename" off}/g;		# Filename - no effect
	$line =~ s/<icon>(.*?)<\/icon>/\{"Icon" on}$1\{"Icon" off}/g;			# Icon labels - enclose in ''
	$line =~ s/<key>(.*?)<\/key>/ucfirst $1/eg;					# Key names - initial capital
	$line =~ s/<menu>(.*?)<\/menu>/\{"Menu" on}$1\{"Menu" off}/g;			# Menu entries - enclose in ''
	$line =~ s/<mouse>(.*?)<\/mouse>/ucfirst $1/eg;					# Mouse buttons - initial capital
	$line =~ s/<param>(.*?)<\/param>/\{"Parameter" on}$1\{"Parameter" off}/g;	# Command parameter - no effect
	$line =~ s/<strong>(.*?)<\/strong>/\{"Strong" on}$1\{"Strong" off}/g;		# Strong Emphasis - To Upper
	$line =~ s/<window>(.*?)<\/window>/\{"Window" on}$1\{"Window" off}/g;		# Window names - no effect

	# Hyperlinks are unsupported, so just remove them.

	$line =~ s/<link.*?>(.*?)<\/link>/$1/g;

	# Finally, expand all the entities out.

	while ($line =~ /&(.*?);/) {
		$line = "$`$entities{$1}$'";
	}

	$line =~ s/\e/&/g; # Restore the &s after the entities have gone.

	# Perform the linewrap on the string, indenting as required.

#  $line .= "\n";
#  $line =~ s/(.{0,72})[\s]+/"$1\n"." "x$indent/eg;

	# Bullet the start of the line if necessary

	if ($line =~ /^<li>/) {
		if ($list_type != 0) {
			if ($list_type == 1) {
				$line =~ s/^<li>\s*/"††è{tab}"/e;
			} elsif ($list_type == 2) {
				$line =~ s/^<li>\s*/sprintf ("%2d. ", $list_value++)."{tab}"/e;
			} elsif ($list_type == 3) {
				$line =~ s/^<li>\s*/sprintf ("%2c) ", 64+$list_value++)."{tab}"/e;
			} elsif ($list_type == 4) {
				$line =~ s/^<li>\s*/sprintf ("%2c) ", 96+$list_value++)."{tab}"/e;
			}
		} else {
			$line =~ s/^<li>\s*//;
		}
	} elsif ($list_type != 0) {
		$line = "{tab}".$line;
	}

	# Remove any trailing whitespace from the line

	$line =~ s/\s*$//;

	print $output "$line\n";
}



# --------------------------------------------------------------------------------------------------
# Stacking functions

sub set_new_list_value
{
	my ($value, $type) = @_;

	push(@list_vals, $list_value);
	$list_value = $value;
	push(@list_types, $list_type);
	$list_type = $type;
}


sub restore_list_value
{
	$list_value = pop(@list_vals);
	$list_type = pop(@list_types);
}


sub set_new_indent
{
	my ($new) = @_;

	push(@indents, $indent);
	$indent = $new;
}


sub restore_indent
{
	$indent = pop(@indents);
}


sub set_new_spacing
{
	my ($new) = @_;

	push(@spacings, $spacing);
	$spacing = $new;
}


sub restore_spacing
{
	$spacing = pop(@spacings);
}


sub set_new_wrapping
{
	my ($new) = @_;

	push(@wrappings, $wrapping);
	$wrapping = $new;
}


sub restore_wrapping
{
	$wrapping = pop(@wrappings);
}


# -------------------------------------------------------------------------------------------------
# Parse a teg, returning a hash containing a set of attribute/value pairs.
# The tag is given an attribute name "tag_name".
#
# Param: $tag			The tag to be parsed (eg. "p name="value"").
# Return:			A hash of tag name and attribute values.

sub parse_tag
{
	my ($tag) = @_;
	my (%attributes, @items, $item, $name, $value);

	# Convert the spaces outside of quotes into \n and split on these.

	$tag =~ s/\s+([^="]*?=)/\n$1/g;
	@items = split(/\n/, $tag);

	foreach $item (@items) {
		($name, $value) = split(/=/, $item);

		if (defined($value) && $value ne "") {
			$value =~ s/^\s*"(.*)"\s*$/$1/; # Strip quotes and external whitespace.
			$attributes{$name} = $value;
		} else {
			$attributes{'tag_name'} = $name;
		}
	}

	return %attributes;
}

